buildscript {
    ext {
        androidProjectCommon = "${rootDir}/submodules/AndroidLib/android_project_common.gradle"
        androidModuleCommon = "${rootDir}/submodules/AndroidLib/android_module_common.gradle"
    }
    apply from: "${androidProjectCommon}"

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

// Add plugin for 'spotless'
plugins {
    id "com.diffplug.gradle.spotless" version "3.27.1"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

spotless {
    kotlin {
        target "**/*.kt"
        ktlint(versions.ktlint)
    }
}

// 版本号规则如下：
// 1. versionNameBase定义为：<主版本号X>.<次版本号Y>.<hotfix版本号Z>
//    其中，<主版本号X>支持任意位数字；<次版本号Y>仅支持最多2位数字；<hotfix版本号Z>仅支持1位数字。
// 2. versionCodeBase定义为：(<X> * 1000 + <Y> * 10 + <Z>
//
// 示例：
//   "1.3.2"   --> 1032
//   "1.13.1"  --> 1131
int majorVersion = 1
int minorVersion = 5
int hotfixVersion = 0

ext.getAppVersionNameBase = {
    return  "${majorVersion}.${minorVersion}.${hotfixVersion}"
}

ext.getAppVersionCodeBase = {
    return majorVersion * 1000 + minorVersion * 10 + hotfixVersion
}

ext {
    versions.minSdk = 21
    versions.ndkVersion = "21.0.6113669"
}
