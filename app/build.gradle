apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "${androidModuleCommon}"
apply from: '../build_common.gradle'

android {
    defaultConfig {
        applicationId "me.ycdev.android.devtools"
        minSdkVersion versions.minSdk
        targetSdkVersion 31

        versionName getAppVersionNameBase() // + build type suffix
        versionCode getAppVersionCodeBase() * 10000 + calculatedVersionCode

        buildConfigField "boolean", "DEBUG_LOG", "false"
    }

    buildFeatures.viewBinding = true

    buildTypes {
        debug {
            versionNameSuffix "-debug"

            buildConfigField "boolean", "DEBUG_LOG", "true"
        }

        release {
            versionNameSuffix "-" + calculatedVersionCode + buildIdSuffix

            proguardFiles getDefaultProguardFile('proguard-android.txt'), "proguard-rules.txt"
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'

        // disable category "Accessibility"
        disable 'ClickableViewAccessibility','ContentDescription','LabelFor'
        disable 'IconMissingDensityFolder'
        disable 'GoogleAppIndexingWarning'

        // for 'src/main/assets/busybox_static'
        disable 'UnsafeNativeCodeLocation'

        // for http://code.google.com/p/android/issues/detail?id=223795
        disable 'PrivateResource'
    }
}

dependencies {
    implementation deps.ycdev.androidBase
    implementation deps.ycdev.androidUi
    implementation project(':devToolsCommon')

    implementation deps.kotlin.stdlib
    implementation deps.kotlin.coroutinesCore
    implementation deps.kotlin.coroutinesAndroid

    implementation deps.androidx.coreKtx
    implementation deps.androidx.material
    implementation deps.androidx.constraintLayout
    implementation deps.androidx.recyclerview
    implementation deps.lifecycle.runtimeKtx
    implementation deps.lifecycle.viewModelKtx
    implementation deps.lifecycle.liveDataKtx
    implementation deps.gson
    implementation deps.timber

    // Dependencies for local unit tests
    testImplementation deps.test.core
    testImplementation deps.test.junit
    testImplementation deps.test.truth
    testImplementation deps.test.mockk

    // Android Testing Support Library's runner and rules
    androidTestImplementation deps.test.runner
    androidTestImplementation deps.test.rules
}
