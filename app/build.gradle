apply plugin: 'com.android.application'

def gitCommitCountCmd = 'git rev-list HEAD --count'
def calculatedVersionCode = gitCommitCountCmd.execute().text.trim().toInteger()

def gitLatestCommitCmd = 'git log --format="%H" -1'
String gitLatestCommitSha1 = gitLatestCommitCmd.execute().text.trim().replace('\"', '')

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "me.ycdev.android.devtools"
        minSdkVersion 15
        targetSdkVersion 24

        versionName "1.3.2"
        versionCode calculatedVersionCode

        buildConfigField "boolean", "DEBUG_LOG", rootProject.ext.DEBUG_LOG
        buildConfigField "boolean", "DISABLE_LOG", rootProject.ext.DISABLE_LOG

        manifestPlaceholders = [ gitCommitSha1:gitLatestCommitSha1 ]

        jackOptions {
            enabled = true
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        androidTestKey {
            storeFile file("apk.keystore")
            storePassword "android"
            keyAlias "android.testkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.androidTestKey
        }

        release {
            signingConfig signingConfigs.androidTestKey

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent,
                    "DevTools-${variant.name}-${defaultConfig.versionName}" +
                            "-${defaultConfig.versionCode}.apk")
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'

        // disable category "Accessibility"
        disable 'ClickableViewAccessibility','ContentDescription','LabelFor'
        disable 'IconMissingDensityFolder'
        disable 'GoogleAppIndexingWarning'

        // for 'src/main/assets/busybox_static'
        disable 'UnsafeNativeCodeLocation'

        // for http://code.google.com/p/android/issues/detail?id=223795
        disable 'PrivateResource'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':archLib')
    compile project(':baseLib')
    compile project(':uiLib')
    compile project(':libsuperuser')

    compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:support-annotations:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:design:${rootProject.ext.supportLibVersion}"

    compile "com.alibaba:fastjson:${rootProject.ext.fastjsonVersion}"

    // Dependencies for local unit tests
    testCompile "junit:junit:${rootProject.ext.junitVersion}"
    testCompile "org.hamcrest:hamcrest-core:${rootProject.ext.hamcrestVersion}"
    testCompile "org.hamcrest:hamcrest-library:${rootProject.ext.hamcrestVersion}"
    testCompile "org.mockito:mockito-core:${rootProject.ext.mockitoVersion}"
    testCompile "org.powermock:powermock-api-mockito:${rootProject.ext.powerMockVersion}"
    testCompile "org.powermock:powermock-module-junit4:${rootProject.ext.powerMockVersion}"

    // Android Testing Support Library's runner and rules
    androidTestCompile "com.android.support.test:runner:${rootProject.ext.runnerVersion}"
    androidTestCompile "com.android.support.test:rules:${rootProject.ext.rulesVersion}"

    // Resolve conflicts between main and test APK
    androidTestCompile "com.android.support:support-annotations:${rootProject.ext.supportLibVersion}"
}
