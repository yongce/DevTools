apply plugin: 'com.android.application'
apply from: "${androidModuleCommon}"

android {
    defaultConfig {
        applicationId "me.ycdev.android.devtools"
        minSdkVersion 21
        targetSdkVersion 29

        versionName getAppVersionNameBase() // + build type suffix
        versionCode getAppVersionCodeBase() * 10000 + calculatedVersionCode

        buildConfigField "boolean", "DEBUG_LOG", "false"
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"

            buildConfigField "boolean", "DEBUG_LOG", "true"
        }

        release {
            versionNameSuffix "-" + calculatedVersionCode + buildIdSuffix

            proguardFiles getDefaultProguardFile('proguard-android.txt'), "proguard-rules.txt"
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'

        // disable category "Accessibility"
        disable 'ClickableViewAccessibility','ContentDescription','LabelFor'
        disable 'IconMissingDensityFolder'
        disable 'GoogleAppIndexingWarning'

        // for 'src/main/assets/busybox_static'
        disable 'UnsafeNativeCodeLocation'

        // for http://code.google.com/p/android/issues/detail?id=223795
        disable 'PrivateResource'
    }
}

dependencies {
    implementation project(':baseLib')
    implementation project(':archLib')
    implementation project(':uiLib')
    implementation project(':common')

    implementation deps.androidx.core
    implementation deps.androidx.material
    implementation deps.gson
    implementation deps.timber

    // Dependencies for local unit tests
    testImplementation deps.test.junit
    testImplementation deps.test.mockitoCore
    testImplementation deps.test.hamcrestCore
    testImplementation deps.test.hamcrestLibrary
    testImplementation deps.test.powermockMockito
    testImplementation deps.test.powermockJunit

    // Android Testing Support Library's runner and rules
    androidTestImplementation deps.test.runner
    androidTestImplementation deps.test.rules
}
