apply plugin: 'com.android.application'
apply from: "${androidModuleCommon}"

// 版本号规则如下：
// 1. versionNameBase定义为：<主版本号X>.<次版本号Y>.<hotfix版本号Z>
//    其中，<主版本号X>支持任意位数字；<次版本号Y>仅支持最多2位数字；<hotfix版本号Z>仅支持1位数字。
// 2. versionCodeBase定义为：(<X> * 1000 + <Y> * 10 + <Z>
//
// 示例：
//   "1.3.2"   --> 1032
//   "1.13.1"  --> 1131
int majorVersion = 1
int minorVersion = 3
int hotfixVersion = 3

String versionNameBase = "${majorVersion}.${minorVersion}.${hotfixVersion}"
int versionCodeBase = majorVersion * 1000 + minorVersion * 10 + hotfixVersion

android {
    defaultConfig {
        applicationId "me.ycdev.android.devtools"
        minSdkVersion 15
        targetSdkVersion 26

        versionName versionNameBase // + build type suffix
        versionCode versionCodeBase * 10000 + calculatedVersionCode

        buildConfigField "boolean", "DEBUG_LOG", "false"
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"

            buildConfigField "boolean", "DEBUG_LOG", "true"
        }

        release {
            versionNameSuffix "-" + calculatedVersionCode + buildIdSuffix

            proguardFiles getDefaultProguardFile('proguard-android.txt'), "proguard-rules.txt"
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'

        // disable category "Accessibility"
        disable 'ClickableViewAccessibility','ContentDescription','LabelFor'
        disable 'IconMissingDensityFolder'
        disable 'GoogleAppIndexingWarning'

        // for 'src/main/assets/busybox_static'
        disable 'UnsafeNativeCodeLocation'

        // for http://code.google.com/p/android/issues/detail?id=223795
        disable 'PrivateResource'
    }
}

dependencies {
    implementation project(':baseLib')
    implementation project(':archLib')
    implementation project(':uiLib')

    implementation deps.support.appcompat
    implementation deps.support.design
    implementation deps.gson

    // Dependencies for local unit tests
    testImplementation deps.test.junit
    testImplementation deps.test.mockitoCore
    testImplementation deps.test.hamcrestCore
    testImplementation deps.test.hamcrestLibrary
    testImplementation deps.test.powermockMockito
    testImplementation deps.test.powermockJunit

    // Android Testing Support Library's runner and rules
    androidTestImplementation deps.test.supportRunner
    androidTestImplementation deps.test.supportRules
}
